import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "checkstyle"
    id "java"
    id "java-gradle-plugin"
    id "java-library"
    id "maven-publish"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://maven.minecraftforge.net/"
    }
    maven {
        url "https://repo.conduit.systems/repository/releases/"
    }
}

dependencies {
    implementation "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "org.apache.logging.log4j:log4j-api:2.14.0"
    implementation "org.apache.logging.log4j:log4j-core:2.14.0"
    compileOnly "net.md-5:SpecialSource:1.10.0"
    compileOnly "cpw.mods:modlauncher:9.0.0"
    compileOnly "org.spongepowered:mixin:0.8.2"
}

group = theGroup
version = theVersion

checkstyle {
    configFile = file("$rootDir/checkstyle.xml")
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property "version", version
    filter(ReplaceTokens, tokens: [VERSION: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

jar {
    manifest {
        attributes(
                "Main-Class" : "systems.conduit.stream.launcher.LauncherStart",
                "Launcher-Agent-Class" : "systems.conduit.stream.launcher.Agent"
        )
    }
}

gradlePlugin {
    plugins {
        stream {
            id = "Stream"
            implementationClass = "systems.conduit.stream.gradle.StreamGradlePlugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username System.getProperty("MAVEN_USER")?:'ci'
                password System.getProperty("MAVEN_PASSWORD")?:'password'
            }
            url "https://repo.conduit.systems/repository/releases/"
        }
    }
}

task install(dependsOn: [publishToMavenLocal])
